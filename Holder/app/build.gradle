apply plugin: 'com.android.application'
apply plugin: 'android-apt'
apply plugin: 'com.google.gms.google-services'

task svnversion {
    description 'Get SVN revision number.'
    new ByteArrayOutputStream().withStream { os ->
        def result = exec {
            executable = 'svnversion'
            standardOutput = os
        }
        ext.revid = os.toString().split(":").last().trim()
    }
}

task ver {
    def props = new Properties()
    file("$projectDir/../../version.properties").withInputStream { props.load(it) }
    ext.versionCode = props.getProperty("versionCode").toInteger()
    ext.versionName = props.getProperty("versionName")
}

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"
    defaultConfig {
        applicationId "com.ul.ts.products.mdlholder"
        minSdkVersion 21
        targetSdkVersion 24
        versionCode ver.versionCode
        versionName ver.versionName
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        release {
            buildConfigField "String", "SVN_VERSION", "\"" + svnversion.revid + "\""
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            buildConfigField "String", "SVN_VERSION", "\"" + svnversion.revid + "\""
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES.txt'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/dependencies.txt'
        exclude 'META-INF/LGPL2.1'
    }

    dexOptions {
        jumboMode = true
    }
}

repositories {
    maven {
        url "https://repo.spring.io/milestone"
    }
    maven {
        url "https://github.com/omadahealth/omada-nexus/raw/master/release"
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    testCompile 'org.mockito:mockito-core:2.2.11'

    // Support libraries
    compile 'com.android.support:appcompat-v7:25.3.0'
    compile 'com.android.support:preference-v7:25.3.0'
    compile 'com.android.support:cardview-v7:25.3.0'

    // Android utilities
    compile 'com.jakewharton:butterknife:8.4.0'
    apt 'com.jakewharton:butterknife-compiler:8.4.0'

    // Bouncy castle crypto helpers
    compile 'org.bouncycastle:bcprov-jdk15on:1.51'

    // ZXing for creating QR codes.
    compile 'com.google.zxing:core:3.2.1'

    // WEB api dependencies
    compile 'org.springframework.android:spring-android-rest-template:2.0.0.M4'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.8.4'

    // Push notifications
    compile 'com.google.firebase:firebase-core:9.8.0'
    compile 'com.google.firebase:firebase-messaging:9.8.0'

    // PIN Lock screen
    compile ('com.github.orangegangsters:lollipin:2.0.0@aar') {
        setTransitive(true);
    }
    compile project(path: ':common')
}
